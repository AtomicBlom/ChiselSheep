buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
// Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

import org.apache.tools.ant.filters.ReplaceTokens

// define the properties file
//ext.configFile = file "gradle.properties"
//configFile.withReader {
//    // read config.  it shall from now on be referenced as simply config or as project.config
//    def prop = new Properties()
//    prop.load(it)
//    project.ext.config = new ConfigSlurper().parse prop
//}

def isCIBuild = hasProperty("CIRevision")
if (isCIBuild) {
    println("Producing a Release build")
} else {
    println("This build will produce an unsactioned release")
}

// combine in the dependency properties file
Properties props = new Properties()
props.load(new FileInputStream("./dependency.properties"))
props.each { prop ->
    project.ext.set(prop.key, prop.value)
}

version = (hasProperty("CIRevision") ? CIRevision : project.mod_version)
group = project.group_name
archivesBaseName = "${project.mod_id}-${project.minecraft_version}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

sourceSets {
    main {
        java {
            srcDirs([
                    'src/main/java',
                    'src/api/java',

                    //Integrations
                    //'src/integrations/chancecubes/java',
                    //'src/integrations/chisel/java',
                    'src/integrations/vanilla/java',
                    'src/integrations/immersiveengineering/java',
                    'src/integrations/silly/java'
            ])
        }
        resources {
            srcDirs([
                    'src/main/resources',

                    //Integrations
                    //'src/integrations/chancecubes/resources',
                    'src/integrations/silly/resources'
            ])
        }

    }
    api {
        java {
            srcDirs(['src/api/java'])
        }
        compileClasspath = sourceSets.main.compileClasspath
    }
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    classifier = 'api'
}
tasks.build.dependsOn apiJar

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'snapshot', version: project.mcp_mappings
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                shearmadness {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                shearmadness {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            //property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            args '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/')

            mods {
                shearmadness {
                    source sourceSets.main
                }
            }
        }
    }
}

task processSource(type: Sync) {
    from sourceSets.main.java
    filter(ReplaceTokens, tokens: [IS_CI_BUILD: isCIBuild.toString()])
    into "$buildDir/src"
}

compileJava {
    source = processSource.outputs
}

repositories { //Access to Gigaherz private maven server for community mappings
    maven { url = 'https://dvs1.progwml6.com/files/maven'  } // JEI & Tinkers
    maven { url = 'https://www.dogforce-games.com/maven/'  } // 1.16 mappings
    maven { url = 'https://maven.blamejared.com/'          } // IE
    maven { url = 'http://maven.tterrag.com/'              } // Chisel
    //maven { url = 'http://maven.theprogrammingturkey.com/' } // Chance Cubes
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft 'net.minecraftforge:forge:' + project.minecraft_version + '-' + project.forge_version
    compile fg.deobf('team.chisel.ctm:CTM:MC' + project.ctm_version)
    //compile fg.deobf("mezz.jei:jei-1.16.2:" + project.jei_version)
    //compile fg.deobf("blusunrize.immersiveengineering:ImmersiveEngineering:" + project.immersive_engineering_version)
    //compile fg.deobf('team.chisel:Chisel:MC' + project.chisel_version)
    //compile fg.deobf(group: 'com.theprogrammingturkey.chancecubes', name: 'ChanceCubes', version: project.chancecubes_version)
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            "Specification-Title": project.mod_id,
            "Specification-Vendor": "atomicblom",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": "${version}",
            "Implementation-Vendor" :"atomicblom",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// Example configuration to allow publishing using the maven-publish task
// This is the preferred method to reobfuscate your jar file
jar.finalizedBy('reobfJar') 
// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing
//publish.dependsOn('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "9999"
        }
    }
}